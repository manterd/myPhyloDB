#########################################################
### Standard Illumina Mothur batch file for myPhyloDB ###
###  Tunable parameters may be changed as necessary.  ###
###                                                   ###
###   Advanced Mothur users, may wish to change the   ###
###   pipeline; however, please note that file names  ###
###       will need to changed accordingly.           ###
#########################################################


### Set directories for input/output files ###
## Tunable parameters [none]
set.dir(input=mothur/temp)


### 
## Tunable parameters [processors]
make.contigs(file=temp.files, processors=6)
summary.seqs(fasta=temp.trim.contigs.fasta)


### 
## Tunable parameters [maxambig, maxlength, processors]
screen.seqs(fasta=temp.trim.contigs.fasta, group=temp.contigs.groups, maxambig=0, maxlength=275, processors=6)
summary.seqs(fasta=temp.trim.contigs.good.fasta)


### 
## Tunable parameters [none]
unique.seqs(fasta=temp.trim.contigs.good.fasta)
summary.seqs(fasta=temp.trim.contigs.good.unique.fasta, name=temp.trim.contigs.good.names)


### Align seqs to silva reference alignment ###
## Tunable parameters [flip, threshold, processors]
align.seqs(fasta=temp.trim.contigs.good.unique.fasta, reference=mothur/reference/align/silva.seed_v119.align, flip=T, threshold=50, processors=6)
summary.seqs(fasta=temp.trim.contigs.good.unique.align, name=temp.trim.contigs.good.names, processors=6)


### Put sequences in same genetic space ###
## Tunable parameters [start, end, optimize, criteria, processors]
screen.seqs(fasta=temp.trim.contigs.good.unique.align, name=temp.trim.contigs.good.names, group=temp.contigs.good.groups, optimize=start-end, criteria=95, processors=6)
summary.seqs(fasta=temp.trim.contigs.good.unique.good.align, name=temp.trim.contigs.good.good.names, processors=6)


### Filter positions with no sequence data ###
## Tunable parameters [vertical, trump, processors]
filter.seqs(fasta=temp.trim.contigs.good.unique.good.align, vertical=T, trump=., processors=6)


### Remove duplicates from fasta file ###
## Tunable parameters [none]
unique.seqs(fasta=temp.trim.contigs.good.unique.good.filter.fasta, name=temp.trim.contigs.good.good.names)
summary.seqs(fasta=temp.trim.contigs.good.unique.good.filter.unique.fasta, name=temp.trim.contigs.good.unique.good.filter.names, processors=6)


### Precluster ###
## Tunable parameters [diffs, processors]
pre.cluster(fasta=temp.trim.contigs.good.unique.good.filter.unique.fasta, name=temp.trim.contigs.good.unique.good.filter.names, group=temp.contigs.good.good.groups, diffs=2, processors=6)
summary.seqs(fasta=temp.trim.contigs.good.unique.good.filter.unique.precluster.fasta, name=temp.trim.contigs.good.unique.good.filter.unique.precluster.names, processors=6)
system(rm *.map)


### Remove chimeras ###
## Tunable parameters [dereplicate]
chimera.uchime(fasta=temp.trim.contigs.good.unique.good.filter.unique.precluster.fasta, name=temp.trim.contigs.good.unique.good.filter.unique.precluster.names, reference=self, dereplicate=F)
remove.seqs(accnos=temp.trim.contigs.good.unique.good.filter.unique.precluster.uchime.accnos, fasta=temp.trim.contigs.good.unique.good.filter.unique.precluster.fasta, name=temp.trim.contigs.good.unique.good.filter.unique.precluster.names, group=temp.contigs.good.good.groups, dups=T)
summary.seqs(fasta=temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.fasta, name=temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.names, processors=6)


### Remove contaminants ###
## Tunable parameters [method, taxon, processors]
classify.seqs(fasta=temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.fasta, name=temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.names, group=temp.contigs.good.good.pick.groups, template=mothur/reference/template/gg_13_5_99.fasta, taxonomy=mothur/reference/taxonomy/gg_13_5_99.pds.tax, method=wang, processors=6)

remove.lineage(fasta=temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.fasta, name=temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.names, group=temp.contigs.good.good.pick.groups, taxonomy=temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.pds.wang.taxonomy, taxon=unknown)
summary.seqs(fasta=temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.pick.fasta, name=temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.pick.names)


### Phylotype ###
## Tunable parameters [none]
phylotype(taxonomy=temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.pds.wang.pick.taxonomy, name=temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.pick.names, label=1)


### Make shared ###
## Tunable parameters [none]
make.shared(list=temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.pds.wang.pick.tx.list, group=temp.contigs.good.good.pick.pick.groups)


### Classify OTU ###
## Tunable parameters [none]
classify.otu(taxonomy=temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.pds.wang.pick.taxonomy, name=temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.pick.names, group=temp.contigs.good.good.pick.pick.groups, list=temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.pds.wang.pick.tx.list)

### Final output files MUST be copied to the following output files
## Linux version
system(cp mothur/temp/temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.pick.fasta mothur/temp/final.fasta)
system(cp mothur/temp/temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.pick.names mothur/temp/final.names)
system(cp mothur/temp/temp.contigs.good.good.pick.pick.groups mothur/temp/final.groups)
system(cp mothur/temp/temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.pds.wang.pick.tx.1.cons.taxonomy mothur/temp/final.taxonomy)
system(cp mothur/temp/temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.pds.wang.pick.tx.shared mothur/temp/final.shared)

## Windows version
system(copy mothur\temp\temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.pick.fasta mothur\temp\final.fasta)
system(copy mothur\temp\temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.pick.names mothur\temp\final.names)
system(copy mothur\temp\temp.contigs.good.good.pick.pick.groups mothur\temp\final.groups)
system(copy mothur\temp\temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.pds.wang.pick.tx.1.cons.taxonomy mothur\temp\final.taxonomy)
system(copy mothur\temp\temp.trim.contigs.good.unique.good.filter.unique.precluster.pick.pds.wang.pick.tx.shared mothur\temp\final.shared)
